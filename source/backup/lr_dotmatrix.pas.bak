unit lr_dotmatrix;

{$mode objfpc}{$H+}

interface

uses
  Classes,
  Forms,
  Controls,
  LResources,
  // lazreport
  LR_Class,
  LR_Prntr,
  LR_View,
  LR_Const,
  // lr_dotmatrix
  lr_dotmatrix_filter,
  lr_dotmatrix_dlg;

type

  { TlrDMReport }

  TlrDMReport = class(TfrReport)
  private
    fDotMatrixReport: boolean;
    function GetDotMatrixConfig: TlrDMConfig;
    procedure SetDotMatrixConfig(aValue: TlrDMConfig);
  public
    constructor Create(AOwner: TComponent); override;
    procedure PrintPreparedReport(const aPageNumbers: string; aCopies: integer);
    procedure ShowPreparedReport;
    procedure ShowReport;
  published
    property DotMatrixConfig: TlrDMConfig read GetDotMatrixConfig write SetDotMatrixConfig;
    property DotMatrixReport: boolean read fDotMatrixReport write fDotMatrixReport;
  end;

  { TlrDMPreview }

  TlrDMPreview = class(TfrPreviewForm)
    procedure PrintBtnClick(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: word; Shift: TShiftState);
  public
    fDoc: Pointer; // => hack
    procedure Show_Modal(aDoc: Pointer);
  end;

procedure Register;

implementation

function IfThen(aCondition: boolean; const aFalseReturn: string; const aTrueReturn: string): string;
begin
  if (aCondition) then
  begin
    Result := aTrueReturn;
  end
  else
  begin
    Result := aFalseReturn;
  end;
end;

procedure Register;
begin
  RegisterComponents('LazReport', [TlrDMReport]);
end;

{ TlrDMReport }

function TlrDMReport.GetDotMatrixConfig: TlrDMConfig;
begin
  Result := vlrDMConfig;
end;

procedure TlrDMReport.SetDotMatrixConfig(aValue: TlrDMConfig);
begin
  vlrDMConfig.Assign(aValue);
end;

constructor TlrDMReport.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  fDotMatrixReport := True;
  // Register dotmatix filter
  frRegisterExportFilter(TlrDMFilter, 'Draft file' + ' (*.drf)', '*.drf');
end;

procedure TlrDMReport.PrintPreparedReport(const aPageNumbers: string; aCopies: integer);
var
  vLoop: integer;
begin
  if (fDotMatrixReport) then
  begin
    CurReport := Self;
    MasterReport := Self;
    Terminated := False;
    PrepareReport;
    for vLoop := 0 to (aCopies - 1) do
    begin
      ExportTo(TlrDMFilter, './tmp.drf');
    end;
    Terminated := True;
  end
  else
  begin
    inherited PrintPreparedReport(aPageNumbers, aCopies);
  end;
end;

procedure TlrDMReport.ShowPreparedReport;
var
  vPreview: TlrDMPreview;
begin
  if (fDotMatrixReport) then
  begin
    CurReport := Self;
    MasterReport := Self;
    DocMode := dmPrinting;
    if (EMFPages.Count > 0) then
    begin
      vPreview := TlrDMPreview.Create(nil);
      vPreview.BorderIcons := vPreview.BorderIcons - [biMinimize];
      vPreview.Caption := IfThen((Title = ''), (SPreview), (SPreview + ' - ' + Title));
      vPreview.Show_Modal(Self);
    end;
  end
  else
  begin
    inherited ShowPreparedReport;
  end;
end;

procedure TlrDMReport.ShowReport;
begin
  if (PrepareReport) then
  begin
    ShowPreparedReport;
  end
  else
  begin
    inherited ShowReport;
  end;
end;

{ TlrDMPreview }

procedure TlrDMPreview.PrintBtnClick(Sender: TObject);
begin
  if (TlrDMReport(fDoc).DotMatrixReport) then
  begin
    lrDMDlg := TlrDMDlg.Create(nil);
    try
      // Auto new page
      lrDMDlg.chkOptionsAutoNewPage.Checked := vlrDMConfig.AutoNewPage;
      lrDMDlg.edtOptionsAutoNewPageLines.Value := vlrDMConfig.AutoNewPageLines;

      // Line spacing
      vlrDMConfig.AddingLineSpacingTo(lrDMDlg.cboOptionsLineSpacing.Items);
      lrDMDlg.cboOptionsLineSpacing.ItemIndex := Ord(vlrDMConfig.LineSpacing);
      lrDMDlg.edtOptionsLineSpacingCustom.Value := vlrDMConfig.LineSpacingCustomValue;

      // List printer
      lrDMDlg.cboPrinter.Items.Assign(Prn.Printers);
      lrDMDlg.cboPrinter.ItemIndex := Prn.PrinterIndex;

      // Show dialog
      if lrDMDlg.ShowModal = mrOk then
      begin
        // Auto new page
        vlrDMConfig.AutoNewPage := lrDMDlg.chkOptionsAutoNewPage.Checked;
        vlrDMConfig.AutoNewPageLines := lrDMDlg.edtOptionsAutoNewPageLines.Value;

        // Line spacing
        vlrDMConfig.LineSpacing := TlrDMLineSpacing(Ord(lrDMDlg.cboOptionsLineSpacing.ItemIndex));
        vlrDMConfig.LineSpacingCustomValue:= lrDMDlg.edtOptionsLineSpacingCustom.Value;

        // Printer Index
        Prn.PrinterIndex := lrDMDlg.cboPrinter.ItemIndex;
        // Print
        TlrDMReport(fDoc).PrintPreparedReport('', lrDMDlg.edtCopies.Value);
      end;
    finally
      lrDMDlg.Free;
    end;
  end
  else
  begin
    inherited PrintBtnClick(Sender);
  end;
end;

procedure TlrDMPreview.FormKeyDown(Sender: TObject; var Key: word; Shift: TShiftState);
begin
  if (ssCtrl in Shift) and (Chr(Key) = 'P') and (PrintBtn.Visible) then
  begin
    PrintBtnClick(nil);
  end
  else
  begin
    inherited FormKeyDown(Sender, Key, Shift);
  end;
end;

procedure TlrDMPreview.Show_Modal(aDoc: Pointer);
begin
  fDoc := aDoc;
  inherited Show_Modal(aDoc);
end;

initialization
  {$I lr_dotmatrix.lrs}

end.
